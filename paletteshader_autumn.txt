// helper function, please ignore
number _hue(number s, number t, number h)
{
	h = mod(h, 1.);
	number six_h = 6.0 * h;
	if (six_h < 1.) return (t-s) * six_h + s;
	if (six_h < 3.) return t;
	if (six_h < 4.) return (t-s) * (4.-six_h) + s;
	return s;
}

// input: vec4(h,s,l,a), with h,s,l,a = 0..1
// output: vec4(r,g,b,a), with r,g,b,a = 0..1
vec4 hsl_to_rgb(vec4 c)
{
	if (c.y == 0)
		return vec4(vec3(c.z), c.a);

	number t = (c.z < .5) ? c.y*c.z + c.z : -c.y*c.z + (c.y+c.z);
	number s = 2.0 * c.z - t;
	return vec4(_hue(s,t,c.x + 1./3.), _hue(s,t,c.x), _hue(s,t,c.x - 1./3.), c.w);
}

// input: vec4(r,g,b,a), with r,g,b,a = 0..1
// output: vec4(h,s,l,a), with h,s,l,a = 0..1
vec4 rgb_to_hsl(vec4 c)
{
	number low = min(c.r, min(c.g, c.b));
	number high = max(c.r, max(c.g, c.b));
	number delta = high - low;
	number sum = high+low;

	vec4 hsl = vec4(.0, .0, .5 * sum, c.a);
	if (delta == .0)
		return hsl;

	hsl.y = (hsl.z < .5) ? delta / sum : delta / (2.0 - sum);

	if (high == c.r)
		hsl.x = (c.g - c.b) / delta;
	else if (high == c.g)
		hsl.x = (c.b - c.r) / delta + 2.0;
	else
		hsl.x = (c.r - c.g) / delta + 4.0;

	hsl.x = mod(hsl.x / 6., 1.);
	return hsl;
}

vec4 effect(vec4 color, Image texture, vec2 texture_coords, vec2 screen_coords) {
  vec4 texturecolor = Texel(texture, texture_coords);
  texturecolor = texturecolor * color;
  vec4 texturehsb = rgb_to_hsl(texturecolor);
  vec4 outc = texturehsb;
  //make color closer to orangey
  if(outc.x > 0.7) outc.x -= 1;
  outc.x -= 0.05;
  outc.x = outc.x * (outc.z); //make color more red if the color is dark
  outc.x += 0.05;
  if(outc.x < 0.0) outc.x += 1;
  //end 
  if(outc.y == 0) outc.x = 0.1;
  outc.y = outc.y + 0.3;
  outc.z = outc.z * 0.9 + 0.1;
  if(abs(0.13 - texturehsb.x) < 0.05) { //extra-bright yellows
    outc.z -= 0.05;// * (abs(0.13 - texturehsb.x) / 0.05); //a bit darker...
    outc.y += 0.1;// * (abs(0.13 - texturehsb.x) / 0.05); //...and a bit brighter
  }
  outc = hsl_to_rgb(outc);
  return outc;
}